import groovy.xml.MarkupBuilder

ext {
    /*versionName = rootProject.ext.android.versionName
    versionCode = rootProject.ext.android.versionCode*/
    versionName = '1.1.2'
    versionCode = 2
    versionInfo = 'App的第1个版本，上线了一些最基础核心的功能.'
    destFile = file('releasesinfo.xml')//指定输出文件
    if (destFile != null && !destFile.exists()) {
        destFile.createNewFile()
    }
}

//创建一个Task，并指定输入输出
task writeTask {
    inputs.property('versionCode', this.versionCode)
    inputs.property('versionName', this.versionName)
    inputs.property('versionInfo', this.versionInfo)
    outputs.file this.destFile
    doLast {
        //将输入的内容写入到输出文件中去
        def data = inputs.getProperties()
        File file = outputs.getFiles().getSingleFile()
        def versionMsg = new VersionMsg(data)
        //将实体对象写入到xml文件中
        def sw = new StringWriter()
        def xmlBuilder = new MarkupBuilder(sw)
        if (file.text != null && file.text.size() <= 0) {
            //没有内容
            xmlBuilder.releases {
                release {
                    versionCode(versionMsg.versionCode)
                    versionName(versionMsg.versionName)
                    versionInfo(versionMsg.versionInfo)
                }
            }
            //直接写入
            file.withWriter { writer -> writer.append(sw.toString())
            }
        } else {
            //已有其它版本内容
            xmlBuilder.release {
                versionCode(versionMsg.versionCode)
                versionName(versionMsg.versionName)
                versionInfo(versionMsg.versionInfo)
            }
            //插入到最后一行前面
            def lines = file.readLines()
            def lengths = lines.size() - 1
            file.withWriter { writer ->
                lines.eachWithIndex { line, index ->
                    if (index != lengths) {
                        writer.append(line + '\r\n')
                    } else if (index == lengths) {
                        writer.append('\r\r\n' + sw.toString() + '\r\n')
                        writer.append(lines.get(lengths))
                    }
                }
            }
        }
    }
}
//信息实体类
class VersionMsg {
    String versionCode
    String versionName
    String versionInfo
}
//通过输入输出来指定Task的执行顺序
task readTask {
    //指定输入文件为上一个task的输出
    inputs.file this.destFile
    doLast {
        //读取输入文件的内容并显示
        def file = inputs.files.singleFile
        println file.text
    }
}

task taskInput {
    setGroup('fimics')
    dependsOn writeTask, readTask//通过依赖指定Task的执行顺序
    doLast {
        println '输入输出任务结束'
    }
}

task handleReleaseFile {
    def srcFile = file('releases.xml')
    def destDir = new File(this.buildDir, 'generated/release/')
    doLast {
        println '开始解析对应的xml文件...'
        destDir.mkdir()
        def releases = new XmlParser().parse(srcFile)
        releases.release.each { releaseNode ->
            //解析每个release结点的内容
            def name = releaseNode.versionName.text()
            def versionCode = releaseNode.versionCode.text()
            def versionInfo = releaseNode.versionInfo.text()
            //创建文件并写入结点数据
            def destFile = new File(destDir, "release-${name}.txt")
            destFile.withWriter { writer -> writer.write("${name} -> ${versionCode} -> ${versionInfo}")
            }
        }
    }
}

task handleReleaseFileTest(dependsOn: handleReleaseFile) {
    def dir = fileTree(this.buildDir.path + 'generated/release/')
    doLast {
        dir.each {
            println 'the file name is:' + it
        }
        println '输出完成...'
    }
}

//task执行顺序
// ./gradlew kjplayer:taskC taskA taskB
task taskA {
    setGroup('fimics')
    doLast {
        println "taskA"
    }
}


task taskB {
    setGroup('fimics')
    mustRunAfter taskA //在执行完taskA 再执行自己的代码
//    shouldRunAfter taskA
    doLast {
        println "taskB"
    }
}

task taskC {
    setGroup('fimics')
    mustRunAfter taskB ////在执行完taskA 再执行自己的代码
    doLast {
        println "taskC"
    }
}


//把自己的task挂接到构建过程中
//5.0 以前
//this.project.afterEvaluate {project->
//    def buildTask = project.tasks.getByName('build')
//    if (buildTask == null) throw GradleException("build task is not found")
//    buildTask.doLast {
//        tasks.writeTask.execute()
//    }
//}

//5.0 以后
this.project.afterEvaluate { project ->
    def buildTask = project.tasks.getByName('build')
    if (buildTask == null) {
        throw GradleScriptException('the build task is not found')
    }
    buildTask.finalizedBy "writeTask"
}


task gitTask {
    group 'fimics'
    println "gitTask"
    doLast {
        String gitRev = 'git rev-parse --short HEAD'.execute(null, project.rootDir).text.trim()
        println gitRev
    }
}
